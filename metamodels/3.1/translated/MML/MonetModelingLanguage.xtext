grammar org.monet.editor.dsl.MonetModelingLanguage with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate monetModelingLanguage "http://www.monet.org/editor/dsl/MonetModelingLanguage"

DomainModel:
	DefinitionModel | ProjectModel | DistributionModel
;

DistributionModel:
	'distribution' name=ValidID 'extends' superType=[ProjectModel|QualifiedName] '{'
		features+=ManifestFeature*
	'}'
;

ProjectModel:
	'project' name=ValidID '{'
		features+=ManifestFeature*
	'}'
;

ManifestFeature:
	Attribute | Property | Define
;

DefinitionModel:
	elements+=Import*
	elements+= PackageDeclaration;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

Code:
	'[' value = STRING ']'
;

PackageDeclaration:
	'package' name=QualifiedName '{'
		
		definition=Definition
	'}';

DefinitionType:
'activity' | 'analysisboard' | 'catalog' | 'collection' | 'container' | 'cube' | 'dashboard' | 'datastore-builder' | 'datastore' | 'desktop' | 'dimension' | 'document' | 'exporter' | 'form' | 'glossary' | 'importer' | 'index' | 'job' | 'kpi' | 'measure-unit' | 'role' | 'sensor' | 'service' | 'tasktray' | 'thesaurus'
;

PropertyTypes:
'field-text' | 'summary' | 'for' | 'external' | 'edit-boolean' | 'door' | 'field-link' | 'rule:link' | 'wait' | 'is-singleton' | 'send-response' | 'customer' | 'edit-number' | 'is-static' | 'rule:form' | 'delegation' | 'is-visible-when-embedded' | 'operation' | 'edit-video' | 'field-composite' | 'rejected' | 'report' | 'is-manual' | 'sorting' | 'feature' | 'partner' | 'edit-picture' | 'taxonomy' | 'place' | 'signature' | 'enroll' | 'is-embedded' | 'is-abstract' | 'field-memo' | 'expiration' | 'parent' | 'check-position' | 'require-partner-context' | 'exit' | 'is-superfield' | 'category' | 'explicit' | 'metric' | 'is-self-generated' | 'enable-services' | 'cube' | 'view' | 'edit-check' | 'field-check' | 'range' | 'self-generated' | 'secondary' | 'contest' | 'line' | 'use' | 'mapping' | 'from-index' | 'field-node' | 'space' | 'links:out' | 'meta' | 'is-environment' | 'is-georeferenced' | 'edit-date' | 'locations' | 'is-required' | 'length' | 'enable-feeders' | 'open-contest' | 'publish' | 'owned-prototypes' | 'internal' | 'listen' | 'shared-prototypes' | 'from-roles' | 'capture-date' | 'edit-text' | 'field-number' | 'is-component' | 'tab:taskboard' | 'tab:trash' | 'field-file' | 'folder' | 'analyze' | 'pattern' | 'tab:news' | 'links:in' | 'rule:view' | 'field-summation' | 'notes' | 'revisions' | 'edit-memo' | 'to' | 'edition' | 'field-date' | 'is-extended' | 'terms' | 'implicit' | 'show' | 'close-contest' | 'allow-other' | 'level' | 'is-prototypable' | 'capture-position' | 'allow-key' | 'primary' | 'request' | 'edit-select' | 'send-job' | 'rule:operation' | 'index' | 'select' | 'is-read-only' | 'allow-search' | 'is-hand-written' | 'allow-history' | 'display' | 'properties' | 'is-readonly' | 'size' | 'disable-users' | 'field-select' | 'field:node' | 'items' | 'contain' | 'is-conditional' | 'rule:node' | 'contestants' | 'filter' | 'stop' | 'indicator' | 'scale' | 'edit-position' | 'measure' | 'attribute' | 'is-extensible' | 'unpublish' | 'add' | 'default-location' | 'shortcut' | 'response' | 'field-uri' | 'parameter' | 'is-negative' | 'allow-add' | 'aborted' | 'federation' | 'rule' | 'field-boolean' | 'back-enable' | 'tab:tasktray' | 'is-default' | 'disable' | 'attachments' | 'is-final' | 'check' | 'is-univocal' | 'provider' | 'signatures' | 'tasks' | 'assign-role' | 'field-serial' | 'reference' | 'tab:roles' | 'is-oust' | 'is-multiple' | 'is-private' | 'dimension' | 'source' | 'term' | 'field-picture' | 'send-request' | 'is-category' | 'is-initial' | 'serial' | 'olap' | 'timeout' | 'step'
;

MethodTypes:
'onArrive' | 'onRequest' | 'onSaved' | 'calculate' | 'onSelectRole' | 'onAssign' | 'onPopulated' | 'onTimeout' | 'onValidate' | 'isEnabled' | 'onResponse' | 'onCreate' | 'onTake' | 'onExportItem' | 'onTermAdded' | 'import' | 'onClosed' | 'onSetup' | 'onChatMessageReceived' | 'onSave' | 'onBack' | 'constructor' | 'onImportItem' | 'onOpened' | 'execute' | 'onTerminate' | 'onAbort' | 'onBuild' | 'onItemRemoved' | 'onTermModified' | 'onSetupComplete' | 'onUnassign' | 'onItemAdded' | 'onFinished' | 'onInit' | 'onInitialize' | 'onSetupJob' | 'onExecute' | 'onSetContext' | 'onSolve' | 'onRemoved' | 'onCreated' | 'onSelectJobRole' | 'isVisible' | 'onSynchronized' | 'onTerminate'
;

AttributeTypes:
'setup-uri' | 'for' | 'tag' | 'highlight' | 'historic' | 'center' | 'operation' | 'help' | 'collaborator' | 'remote-link' | 'role' | 'feature' | 'socket-uri' | 'add-flag' | 'template' | 'unlock' | 'form' | 'alert' | 'job' | 'parent' | 'category' | 'metric' | 'view' | 'latitude' | 'dashboard' | 'regexp' | 'position' | 'range' | 'line-below' | 'deploy-uri' | 'exclusive' | 'link' | 'contest' | 'line' | 'use' | 'goto' | 'collection' | 'mode' | 'version' | 'sort-by' | 'title' | 'min' | 'max' | 'interpolation' | 'factor' | 'operator' | 'history' | 'field' | 'footer' | 'chart' | 'kpi' | 'show-text' | 'to' | 'root' | 'show' | 'type' | 'datastore' | 'after' | 'height' | 'component' | 'description' | 'request' | 'when' | 'tab:environment' | 'measure-unit' | 'value' | 'longitude' | 'key' | 'index' | 'width' | 'label' | 'format' | 'code' | 'ontology' | 'script' | 'depth' | 'picture' | 'node' | 'default' | 'classificate' | 'exporter' | 'indicator' | 'scale' | 'measure' | 'children' | 'attribute' | 'shortcut-view' | 'resolution' | 'from' | 'shortcut' | 'response' | 'author' | 'flatten' | 'layout' | 'name' | 'sort-mode' | 'activity' | 'icon' | 'precision' | 'definition' | 'task' | 'provider' | 'message' | 'dimension' | 'source' | 'sibling' | 'take' | 'tab:dashboard' | 'context' | 'target' | 'subtitle' | 'service' | 'classifier' | 'with-view' | 'secret' | 'user' | 'field:file' | 'formula'
;

Definition:
	code=Code?
	abstract?='abstract'? extensible?='extensible'? 'definition' name=ValidID 'is' definitionType=DefinitionType ('extends' superType=[Definition|QualifiedName])? ('replace' replaceSuperType=[Definition|QualifiedName])? '{'
		features+=Feature*
	'}'
;

Feature:
	Attribute | Definition | Property | Schema | Method | Function | Variable
;

Attribute:
id=AttributeTypes '=' value=AttributeValue
;

AttributeValue:
StringLiteral | IntLiteral | FloatLiteral | DoubleLiteral | EnumLiteral | XTReference | LocalizedText | Resource | ExpressionLiteral | TimeLiteral
;

LocalizedText:
'text' value=(ID | ENUM) ';'
;

Resource:
  type=('data' | 'help' | 'image' | 'template' | 'layout') value=(ID | ENUM) ';'
;

StringLiteral:
value=STRING ';';

TimeLiteral:
	value=TIME ';'
;

IntLiteral:
	negative?='-'? value=INT ';'
;

FloatLiteral:
	negative?='-'? value=FLOAT ';'
;

DoubleLiteral:
	negative?='-'? value=DOUBLE ';'
;

ExpressionLiteral
:value=XBlockExpression;

Referenciable:
Property | Definition
;

XTReference:
'ref' value=[Referenciable|QualifiedName] ';'
;

EnumLiteral:
value=ENUM ';'
;

terminal ENUM : ('A'..'Z'|'_')('0'..'9'|'A'..'Z'|'_')*;
terminal FLOAT returns ecore::EFloat : INT '.' INT 'F';
terminal DOUBLE returns ecore::EDouble : INT '.' INT;
terminal TIME : ((('0'..'9')+ ':')? ((('0'..'1')('0'..'9')) | (('2')('0'..'3'))) ':')? ('0'..'5')('0'..'9') ':' ('0'..'5')('0'..'9') (',' ('0'..'9')('0'..'9')('0'..'9'))?;

Property:
	code=Code?
	id=PropertyTypes name=ValidID? ('{' 
		features+=PropertyFeature*
	'}' | ';')
;

PropertyFeature:
	Attribute | Property | Method
;

Method:
	id=MethodTypes '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' body=XBlockExpression
;

Variable:
	'var' name=ValidID 'as' type=JvmTypeReference ';'
;

Define:
	'define' name=ValidID '=' value=STRING ';'
;

Schema:
	id='schema' '{'  
	 	properties+=SchemaFeature*
	 '}'
;

SchemaFeature:
	SchemaSection | SchemaProperty
;

SchemaSection:
	(many ?= 'many')? id=ID '{' features += SchemaFeature* '}'
;

SchemaProperty:
	(many ?= 'many')? id=ID source=SchemaPropertyOfValue
;

SchemaPropertyOfValue:
	type = JvmParameterizedTypeReference (body = XBlockExpression | ';')
;
		
Function:
	'function' name=ValidID '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':' type=JvmTypeReference)? 
		body=XBlockExpression;

QualifiedNameWithWildcard:
	QualifiedName  ('.' '*')?;


