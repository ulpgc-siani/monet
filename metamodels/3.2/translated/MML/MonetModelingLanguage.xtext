grammar org.monet.editor.dsl.MonetModelingLanguage with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate monetModelingLanguage "http://www.monet.org/editor/dsl/MonetModelingLanguage"

DomainModel:
	DefinitionModel | ProjectModel | DistributionModel
;

DistributionModel:
	'distribution' name=ValidID 'extends' superType=[ProjectModel|QualifiedName] '{'
		features+=ManifestFeature*
	'}'
;

ProjectModel:
	'project' name=ValidID '{'
		features+=ManifestFeature*
	'}'
;

ManifestFeature:
	Attribute | Property | Define | Method
;

DefinitionModel:
	elements+=Import*
	elements+= PackageDeclaration;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

Code:
	'[' value = STRING ']'
;

PackageDeclaration:
	'package' name=QualifiedName '{'
		
		definition=Definition
	'}';

DefinitionType:
'activity' | 'catalog' | 'collection' | 'container' | 'dashboard' | 'datastore-builder' | 'datastore' | 'desktop' | 'document' | 'exporter' | 'form' | 'glossary' | 'importer' | 'index' | 'job' | 'measure-unit' | 'role' | 'sensor' | 'service' | 'tasktray' | 'thesaurus'
;

PropertyTypes:
'parent' | 'select' | 'field-picture' | 'allow-locations' | 'aborted' | 'allow-add' | 'is-component' | 'taxonomy' | 'signatures' | 'is-static' | 'tab:taskboard' | 'terms' | 'edit-text' | 'default-location' | 'cube' | 'is-default' | 'tab:trash' | 'require-confirmation' | 'is-environment' | 'tasks' | 'edit-memo' | 'mapping' | 'field-summation' | 'links:out' | 'capture-date' | 'edit-video' | 'rule:view' | 'field-boolean' | 'index' | 'self-generated' | 'confirmation' | 'back-enable' | 'secondary' | 'explicit' | 'allow-edit' | 'exit' | 'is-initial' | 'folder' | 'enable-history' | 'size' | 'stop' | 'meta' | 'is-extended' | 'analyze' | 'field-date' | 'allow-less-precision' | 'enroll' | 'items' | 'allow-search' | 'primary' | 'implicit' | 'is-univocal' | 'wait' | 'internal' | 'attachments' | 'send-response' | 'signature' | 'is-singleton' | 'field-number' | 'pattern' | 'edit-position' | 'range' | 'field-uri' | 'assign-role' | 'is-private' | 'feature' | 'edit-check' | 'unpublish' | 'term' | 'place' | 'attribute' | 'enable-services' | 'capture-position' | 'federation' | 'contain' | 'edit-select' | 'is-embedded' | 'display' | 'field-node' | 'is-email' | 'edit-date' | 'filter' | 'from-roles' | 'field-link' | 'tab:roles' | 'is-conditional' | 'is-final' | 'partner' | 'metric' | 'disable' | 'report' | 'is-abstract' | 'to' | 'tab:news' | 'operation' | 'is-self-generated' | 'is-breadcrumbs-disabled' | 'send-request' | 'indicator' | 'field-text' | 'is-required' | 'door' | 'rule:node' | 'notes' | 'use' | 'edit-picture' | 'source' | 'space' | 'listen' | 'reference' | 'view' | 'is-category' | 'is-multiple' | 'is-prototypable' | 'is-extensible' | 'is-hand-written' | 'edit-boolean' | 'check-position' | 'allow-history' | 'add' | 'field-composite' | 'contest' | 'level' | 'is-readonly' | 'shared-prototypes' | 'send-job' | 'is-hidden' | 'external' | 'enable-feeders' | 'field-memo' | 'disable-edition' | 'is-visible-when-embedded' | 'secondary:location' | 'edit-number' | 'expiration' | 'is-external-fed' | 'rule:form' | 'links:in' | 'boundary' | 'request' | 'allow-key' | 'is-read-only' | 'is-negative' | 'is-georeferenced' | 'from-index' | 'rejected' | 'line' | 'for' | 'show' | 'scale' | 'edition' | 'allow-other' | 'timeout' | 'owned-prototypes' | 'shortcut' | 'is-oust' | 'provider' | 'field:node' | 'sorting' | 'revisions' | 'dimension' | 'summary' | 'tab:tasktray' | 'center' | 'is-background' | 'length' | 'is-profile-photo' | 'field-check' | 'disable-users' | 'is-collapsible' | 'field-file' | 'is-manual' | 'field-select' | 'serial' | 'response' | 'publish' | 'olap' | 'rule:link' | 'rule:operation' | 'locations' | 'step' | 'field-serial' | 'require-partner-context' | 'category' | 'delegation' | 'properties' | 'customer'
;

MethodTypes:
'onChatMessageReceived' | 'onPopulated' | 'isVisible' | 'onTake' | 'when' | 'onExportItem' | 'onResponse' | 'onSave' | 'onSelectJobRoleComplete' | 'onInit' | 'onRemoved' | 'calculate' | 'onSetupComplete' | 'onAbort' | 'isEnabled' | 'constructor' | 'onSetupJobComplete' | 'execute' | 'onSignsComplete' | 'onTermAdded' | 'onSolve' | 'onImportItem' | 'onFinished' | 'onOpened' | 'onSelectJobRole' | 'onRequest' | 'import' | 'onSaved' | 'onTerminate' | 'onCancel' | 'onValidate' | 'onSynchronized' | 'onUnassign' | 'onSetup' | 'onItemAdded' | 'onSign' | 'onCreated' | 'onReceiveEvent' | 'onArrive' | 'onClosed' | 'onCreate' | 'onSetContext' | 'onExecute' | 'onAssign' | 'onItemRemoved' | 'onTermModified' | 'onTimeout' | 'onBack' | 'onBuild' | 'onInitialize' | 'onTerminate' | 'onSetupJob' | 'onSelectRole'
;

AttributeTypes:
'parent' | 'datastore' | 'type' | 'resolution' | 'when' | 'collaborator' | 'tab:environment' | 'shortcut-view' | 'children' | 'classifier' | 'context' | 'line-below' | 'dashboard' | 'height' | 'index' | 'version' | 'script' | 'node' | 'show-text' | 'field' | 'subtitle' | 'template' | 'regexp' | 'role' | 'latitude' | 'icon' | 'range' | 'secret' | 'layer' | 'exporter' | 'default' | 'goto' | 'min' | 'feature' | 'after' | 'attribute' | 'key' | 'max' | 'uri' | 'picture' | 'help' | 'component' | 'depth' | 'metric' | 'width' | 'measure-unit' | 'operation' | 'indicator' | 'use' | 'tab:dashboard' | 'precision' | 'source' | 'operator' | 'flatten' | 'mode' | 'view' | 'add-flag' | 'limit' | 'tag' | 'ontology' | 'longitude' | 'use:amount' | 'unlock' | 'author' | 'format' | 'collection' | 'history' | 'field:file' | 'take' | 'task' | 'sort-by' | 'name' | 'position' | 'job' | 'request' | 'code' | 'footer' | 'purpose' | 'line' | 'timezone' | 'link' | 'for' | 'show' | 'scale' | 'description' | 'edition' | 'title' | 'highlight' | 'setup-uri' | 'shortcut' | 'provider' | 'design' | 'root' | 'definition' | 'factor' | 'layout:extended' | 'value' | 'use:y' | 'amount' | 'use:x' | 'center' | 'zoom' | 'label' | 'with-view' | 'message' | 'target' | 'layout' | 'interpolation' | 'sibling' | 'sort-mode' | 'form' | 'service' | 'response' | 'marker-icon' | 'formula' | 'deploy-uri' | 'user'
;

Definition:
	code=Code?
	abstract?='abstract'? extensible?='extensible'? 'definition' name=ValidID 'is' definitionType=DefinitionType ('extends' superType=[Definition|QualifiedName])? ('replace' replaceSuperType=[Definition|QualifiedName])? '{'
		features+=Feature*
	'}'
;

Feature:
	Attribute | Definition | Property | Schema | Method | Function | Variable
;

Attribute:
id=AttributeTypes '=' value=AttributeValue
;

AttributeValue:
StringLiteral | IntLiteral | FloatLiteral | DoubleLiteral | EnumLiteral | XTReference | LocalizedText | Resource | ExpressionLiteral | TimeLiteral
;

LocalizedText:
'text' value=(ID | ENUM) ';'
;

Resource:
  type=('data' | 'help' | 'image' | 'template' | 'layout') value=(ID | ENUM) ';'
;

StringLiteral:
value=STRING ';';

TimeLiteral:
	value=TIME ';'
;

IntLiteral:
	negative?='-'? value=INT ';'
;

FloatLiteral:
	negative?='-'? value=FLOAT ';'
;

DoubleLiteral:
	negative?='-'? value=DOUBLE ';'
;

ExpressionLiteral
:value=XBlockExpression;

Referenciable:
Property | Definition
;

XTReference:
'ref' value=[Referenciable|QualifiedName] ';'
;

EnumLiteral:
value=ENUM ';'
;

terminal ENUM : ('A'..'Z'|'_')('0'..'9'|'A'..'Z'|'_')*;
terminal FLOAT returns ecore::EFloat : INT '.' INT 'F';
terminal DOUBLE returns ecore::EDouble : INT '.' INT;
terminal TIME : ((('0'..'9')+ ':')? ((('0'..'1')('0'..'9')) | (('2')('0'..'3'))) ':')? ('0'..'5')('0'..'9') ':' ('0'..'5')('0'..'9') (',' ('0'..'9')('0'..'9')('0'..'9'))?;

Property:
	code=Code?
	id=PropertyTypes name=ValidID? ('{' 
		features+=PropertyFeature*
	'}' | ';')
;

PropertyFeature:
	Attribute | Property | Method
;

Method:
	id=MethodTypes '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' body=XBlockExpression
;

Variable:
	'var' name=ValidID 'as' type=JvmTypeReference ';'
;

Define:
	'define' name=ValidID '=' value=STRING ';'
;

Schema:
	id='schema' '{'  
	 	properties+=SchemaFeature*
	 '}'
;

SchemaFeature:
	SchemaSection | SchemaProperty
;

SchemaSection:
	(many ?= 'many')? id=ID '{' features += SchemaFeature* '}'
;

SchemaProperty:
	(many ?= 'many')? id=ID source=SchemaPropertyOfValue
;

SchemaPropertyOfValue:
	type = JvmParameterizedTypeReference (body = XBlockExpression | ';')
;
		
Function:
	'function' name=ValidID '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':' type=JvmTypeReference)? 
		body=XBlockExpression;

QualifiedNameWithWildcard:
	QualifiedName  ('.' '*')?;


