grammar org.monet.editor.dsl.MonetModelingLanguage with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate monetModelingLanguage "http://www.monet.org/editor/dsl/MonetModelingLanguage"

DomainModel:
	DefinitionModel | ProjectModel | DistributionModel
;

DistributionModel:
	'distribution' name=ValidID 'extends' superType=[ProjectModel|QualifiedName] '{'
		features+=ManifestFeature*
	'}'
;

ProjectModel:
	'project' name=ValidID '{'
		features+=ManifestFeature*
	'}'
;

ManifestFeature:
	Attribute | Property | Define | Method
;

DefinitionModel:
	elements+=Import*
	elements+= PackageDeclaration;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

Code:
	'[' value = STRING ']'
;

PackageDeclaration:
	'package' name=QualifiedName '{'
		
		definition=Definition
	'}';

DefinitionType:
'activity' | 'catalog' | 'collection' | 'container' | 'dashboard' | 'datastore-builder' | 'datastore' | 'desktop' | 'document' | 'exporter' | 'form' | 'glossary' | 'importer' | 'index' | 'job' | 'measure-unit' | 'role' | 'sensor' | 'service' | 'tasktray' | 'thesaurus'
;

PropertyTypes:
'parent' | 'field-picture' | 'select' | 'allow-locations' | 'aborted' | 'allow-add' | 'is-component' | 'taxonomy' | 'signatures' | 'is-static' | 'tab:taskboard' | 'terms' | 'edit-text' | 'default-location' | 'recent-task' | 'is-default' | 'tab:trash' | 'cube' | 'require-confirmation' | 'tasks' | 'is-environment' | 'edit-memo' | 'field-summation' | 'mapping' | 'links:out' | 'edit-video' | 'capture-date' | 'field-boolean' | 'rule:view' | 'index' | 'self-generated' | 'back-enable' | 'confirmation' | 'secondary' | 'explicit' | 'allow-edit' | 'exit' | 'is-initial' | 'enable-history' | 'folder' | 'size' | 'stop' | 'meta' | 'is-extended' | 'analyze' | 'field-date' | 'allow-less-precision' | 'orders' | 'enroll' | 'allow-search' | 'items' | 'primary' | 'is-univocal' | 'implicit' | 'internal' | 'wait' | 'send-response' | 'attachments' | 'signature' | 'is-singleton' | 'pattern' | 'field-number' | 'edit-position' | 'range' | 'field-uri' | 'assign-role' | 'is-private' | 'edit-check' | 'feature' | 'unpublish' | 'add-operation' | 'term' | 'attribute' | 'place' | 'enable-services' | 'capture-position' | 'federation' | 'contain' | 'edit-select' | 'is-embedded' | 'display' | 'field-node' | 'is-email' | 'edit-date' | 'filter' | 'field-link' | 'from-roles' | 'toolbar' | 'tab:roles' | 'is-conditional' | 'is-final' | 'partner' | 'metric' | 'disable' | 'report' | 'is-abstract' | 'to' | 'tab:news' | 'is-self-generated' | 'operation' | 'is-breadcrumbs-disabled' | 'send-request' | 'indicator' | 'field-text' | 'is-required' | 'door' | 'rule:node' | 'notes' | 'use' | 'edit-picture' | 'source' | 'listen' | 'space' | 'reference' | 'view' | 'is-category' | 'is-multiple' | 'is-extensible' | 'is-hand-written' | 'is-prototypable' | 'edit-boolean' | 'check-position' | 'allow-history' | 'add' | 'field-composite' | 'contest' | 'level' | 'is-readonly' | 'shared-prototypes' | 'send-job' | 'external' | 'is-hidden' | 'field-memo' | 'enable-feeders' | 'disable-edition' | 'is-visible-when-embedded' | 'edit-number' | 'secondary:location' | 'expiration' | 'rule:form' | 'is-external-fed' | 'links:in' | 'boundary' | 'allow-key' | 'is-read-only' | 'request' | 'is-negative' | 'is-georeferenced' | 'from-index' | 'rejected' | 'line' | 'show' | 'for' | 'edition' | 'scale' | 'allow-other' | 'timeout' | 'owned-prototypes' | 'is-oust' | 'shortcut' | 'provider' | 'sorting' | 'field:node' | 'revisions' | 'dimension' | 'summary' | 'tab:tasktray' | 'center' | 'is-background' | 'length' | 'is-profile-photo' | 'field-check' | 'disable-users' | 'is-collapsible' | 'field-file' | 'field-select' | 'serial' | 'is-manual' | 'response' | 'publish' | 'olap' | 'rule:link' | 'rule:operation' | 'field-serial' | 'locations' | 'location' | 'step' | 'require-partner-context' | 'delegation' | 'category' | 'properties' | 'customer'
;

MethodTypes:
'onChatMessageReceived' | 'onPopulated' | 'isVisible' | 'onTake' | 'when' | 'onExportItem' | 'onResponse' | 'onSelectJobRoleComplete' | 'onSave' | 'onInit' | 'onRemoved' | 'onSetupComplete' | 'calculate' | 'onAbort' | 'isEnabled' | 'constructor' | 'onSetupJobComplete' | 'execute' | 'onSignsComplete' | 'onTermAdded' | 'onSolve' | 'onImportItem' | 'onFinished' | 'onOpened' | 'onSelectJobRole' | 'import' | 'onRequest' | 'onSaved' | 'onTerminate' | 'onCancel' | 'onValidate' | 'onSynchronized' | 'onSetup' | 'onUnassign' | 'onItemAdded' | 'onSign' | 'onCreated' | 'onReceiveEvent' | 'onArrive' | 'onCreate' | 'onClosed' | 'onSetContext' | 'onExecute' | 'onAssign' | 'onItemRemoved' | 'onTimeout' | 'onTermModified' | 'onBack' | 'onBuild' | 'onInitialize' | 'onSetupJob' | 'onTerminate' | 'onSelectRole'
;

AttributeTypes:
'parent' | 'datastore' | 'type' | 'when' | 'resolution' | 'collaborator' | 'tab:environment' | 'shortcut-view' | 'children' | 'enable' | 'context' | 'classifier' | 'line-below' | 'dashboard' | 'height' | 'index' | 'version' | 'script' | 'node' | 'show-text' | 'field' | 'subtitle' | 'template' | 'regexp' | 'role' | 'latitude' | 'icon' | 'range' | 'secret' | 'layer' | 'default' | 'exporter' | 'min' | 'goto' | 'feature' | 'attribute' | 'after' | 'key' | 'max' | 'uri' | 'picture' | 'deploy-path' | 'help' | 'component' | 'depth' | 'metric' | 'width' | 'measure-unit' | 'operation' | 'indicator' | 'shortcut-definition' | 'tab:dashboard' | 'use' | 'precision' | 'source' | 'operator' | 'mode' | 'flatten' | 'view' | 'add-flag' | 'limit' | 'tag' | 'ontology' | 'longitude' | 'use:amount' | 'unlock' | 'author' | 'format' | 'collection' | 'history' | 'field:file' | 'take' | 'task' | 'sort-by' | 'name' | 'position' | 'job' | 'request' | 'code' | 'purpose' | 'footer' | 'timezone' | 'line' | 'link' | 'for' | 'show' | 'edition' | 'description' | 'scale' | 'title' | 'highlight' | 'setup-uri' | 'shortcut' | 'provider' | 'root' | 'design' | 'definition' | 'factor' | 'value' | 'layout:extended' | 'use:y' | 'amount' | 'use:x' | 'center' | 'zoom' | 'label' | 'with-view' | 'message' | 'target' | 'layout' | 'interpolation' | 'sibling' | 'form' | 'sort-mode' | 'service' | 'response' | 'marker-icon' | 'formula' | 'deploy-uri' | 'user'
;

Definition:
	code=Code?
	abstract?='abstract'? extensible?='extensible'? 'definition' name=ValidID 'is' definitionType=DefinitionType ('extends' superType=[Definition|QualifiedName])? ('replace' replaceSuperType=[Definition|QualifiedName])? '{'
		features+=Feature*
	'}'
;

Feature:
	Attribute | Definition | Property | Schema | Method | Function | Variable
;

Attribute:
id=AttributeTypes '=' value=AttributeValue
;

AttributeValue:
StringLiteral | IntLiteral | FloatLiteral | DoubleLiteral | EnumLiteral | XTReference | LocalizedText | Resource | ExpressionLiteral | TimeLiteral
;

LocalizedText:
'text' value=(ID | ENUM) ';'
;

Resource:
  type=('data' | 'help' | 'image' | 'template' | 'layout') value=(ID | ENUM) ';'
;

StringLiteral:
value=STRING ';';

TimeLiteral:
	value=TIME ';'
;

IntLiteral:
	negative?='-'? value=INT ';'
;

FloatLiteral:
	negative?='-'? value=FLOAT ';'
;

DoubleLiteral:
	negative?='-'? value=DOUBLE ';'
;

ExpressionLiteral
:value=XBlockExpression;

Referenciable:
Property | Definition
;

XTReference:
'ref' value=[Referenciable|QualifiedName] ';'
;

EnumLiteral:
value=ENUM ';'
;

terminal ENUM : ('A'..'Z'|'_')('0'..'9'|'A'..'Z'|'_')*;
terminal FLOAT returns ecore::EFloat : INT '.' INT 'F';
terminal DOUBLE returns ecore::EDouble : INT '.' INT;
terminal TIME : ((('0'..'9')+ ':')? ((('0'..'1')('0'..'9')) | (('2')('0'..'3'))) ':')? ('0'..'5')('0'..'9') ':' ('0'..'5')('0'..'9') (',' ('0'..'9')('0'..'9')('0'..'9'))?;

Property:
	code=Code?
	id=PropertyTypes name=ValidID? ('{' 
		features+=PropertyFeature*
	'}' | ';')
;

PropertyFeature:
	Attribute | Property | Method
;

Method:
	id=MethodTypes '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' body=XBlockExpression
;

Variable:
	'var' name=ValidID 'as' type=JvmTypeReference ';'
;

Define:
	'define' name=ValidID '=' value=STRING ';'
;

Schema:
	id='schema' '{'  
	 	properties+=SchemaFeature*
	 '}'
;

SchemaFeature:
	SchemaSection | SchemaProperty
;

SchemaSection:
	(many ?= 'many')? id=ID '{' features += SchemaFeature* '}'
;

SchemaProperty:
	(many ?= 'many')? id=ID source=SchemaPropertyOfValue
;

SchemaPropertyOfValue:
	type = JvmParameterizedTypeReference (body = XBlockExpression | ';')
;
		
Function:
	'function' name=ValidID '(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':' type=JvmTypeReference)? 
		body=XBlockExpression;

QualifiedNameWithWildcard:
	QualifiedName  ('.' '*')?;


